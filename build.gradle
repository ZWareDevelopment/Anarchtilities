buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

group = project.modGroup
version = project.modVersion

compileJava {
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

minecraft {
    version = project.forgeVersion
    mappings = project.mcpVersion
    runDir = 'run'
    coreMod = 'dev.zihasz.anarchtilities.core.Loader'
    makeObfSourceJar = false
}

repositories {
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url = 'https://repo.viaversion.com' }
}

dependencies {
    compile('org.spongepowered:mixin:0.8.2') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }

    compile 'com.github.2b2t-Utilities:emoji-api:master-SNAPSHOT'
    compile 'com.github.2b2t-Utilities:2b2tclantags-api:master-SNAPSHOT'
    compile 'com.thealtening.auth:auth:3.0.2'
    compile 'com.thealtening.api:api:4.1.0'
    compile 'com.viaversion:viaversion:4.0.1'
    compile 'com.viaversion:viabackwards:4.0.1'
    compile 'org.yaml:snakeyaml:1.29'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) { exclude 'mcmod.info' }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    dependencies {
        include('org.spongepowered:mixin')
        include('com.github.2b2t-Utilities:emoji-api')
        include('com.github.2b2t-Utilities:2b2tclantags-api')
        include('com.thealtening.auth:auth')
        include('com.thealtening.api')
        include('com.viaversion:viaversion')
        include('com.viaversion:viabackwards')
        include('org.yaml:snakeyaml')
    }
    exclude 'dummyThing'
    exclude 'LICENSE'
    classifier = 'release'
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, 'mixins.anarchtilities.refmap.json'
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

jar {
    manifest {
        attributes(
                'MixinConfigs': 'mixins.anarchtilities.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLCorePluginContainsFMLMod': 'true',
                'FMLCorePlugin': 'dev.zihasz.anarchtilities.core.Loader',
                'ForceLoadAsMod': 'true',
                'FMLAT': 'anarchtilities_at.cfg'
        )
    }
}

build.dependsOn(shadowJar)
